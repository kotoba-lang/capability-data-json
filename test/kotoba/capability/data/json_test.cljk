(ns kotoba.capability.data.json-test
  (:require [clojure.test :refer [deftest is]]
            [clojure.java.io :as io]
            [kotoba.capability.data.json :as capability]
            [kotoba.capability.data.json.provider :as provider]
            [kotoba.core.capability-repository :as repository]
            [kotoba.core.contracts :as contracts])
  (:import [java.security MessageDigest]))

(defn- sha256-file [f]
  (let [md (MessageDigest/getInstance "SHA-256")
        bytes (.digest md (.readAllBytes (io/input-stream f)))]
    (apply str (map #(format "%02x" (bit-and % 0xff)) bytes))))

(deftest manifest-conforms-as-reference-implemented
  (is (= :reference-implemented (:capability/provider-status capability/manifest)))
  (is (= "data/json" (:capability/id capability/manifest)))
  (is (= "bafyreia2fucycy5w6nmxp3j473pz5vzamg2yisjiog6jw7bzoeqzqcprfq"
         (:capability/definition-cid capability/manifest)))
  (is (= [] (repository/validate-manifest (contracts/capability-contract) capability/manifest))))

(deftest artifact-sha256-matches-bytes
  (let [path (io/file "artifacts/provider.core.wasm")
        declared (get-in capability/manifest [:capability/artifact :sha256])]
    (is (.isFile path))
    (is (= declared (sha256-file path)))))

(deftest artifact-exports-match-host-abi
  (let [exports (get-in capability/manifest [:capability/artifact :exports])]
    (is (= {"json_encode" {:params [:i32 :i32 :i32 :i32], :result :i32}
            "json_extract_field" {:params [:i32 :i32 :i32 :i32 :i32 :i32], :result :i32}}
           exports))))

(deftest jvm-encode-and-extract
  (let [exports (provider/host-exports)
        enc (first (filter #(= "json_encode" (:field %)) exports))
        ext (first (filter #(= "json_extract_field" (:field %)) exports))
        encode (:fn enc)
        extract (:fn ext)]
    (is (= 2 (count exports)))
    (is (= "{}" (encode "")))
    (is (= "{\"a\":\"b\"}" (encode "a\tb")))
    (is (= "{\"a\":\"b\",\"hello\":\"world\"}" (encode "a\tb\nhello\tworld")))
    (is (= "tok123" (extract "{\"accessJwt\":\"tok123\",\"uri\":\"at://x\"}" "accessJwt")))
    (is (= "at://x" (extract "{\"accessJwt\":\"tok123\",\"uri\":\"at://x\"}" "uri")))
    (is (nil? (extract "{\"a\":\"b\"}" "missing")))
    (is (= "a\"b" (extract "{\"x\":\"a\\\"b\"}" "x")))))
